@Metadata.layer: #CUSTOMER

@UI.headerInfo: { typeNamePlural: 'Travels',
                  typeName: 'Travel',
                  title.value: 'TravelId',
                  description.value: 'Description' }

@UI.presentationVariant: [ { sortOrder: [ { by: 'BeginDate', direction: #DESC } ] } ]

annotate view ZC_054906_TravelTP with

{
  /* Facets */
  @UI.facet: [ { position: 10, targetQualifier: 'TravelDetails', label: 'Travel Details', type: #FIELDGROUP_REFERENCE },
               { position: 20, targetQualifier: 'AdminData', label: 'Administrative Data', type: #FIELDGROUP_REFERENCE },
               { position: 30, targetElement: '_Bookings', label: 'Bookings', type: #LINEITEM_REFERENCE } ]
  @UI.fieldGroup: [ { position: 10, qualifier: 'AdminData' } ]
 
 /* Actions */
  @UI.identification: [ { position: 10,
                          dataAction: 'MaintainBookingFee',
                          label: 'Maintain Booking Fee',
                          type: #FOR_ACTION } ]  
  @UI.lineItem: [ { position: 10, dataAction: 'ShowTestMessage', label: 'Show Test Message', type: #FOR_ACTION },
                  { position: 20, dataAction: 'CancelTravel',    label: 'Cancel Travel',     type: #FOR_ACTION } ]

  /* Fields */
  TravelUuid;

  @UI.fieldGroup: [ { position: 10, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 10 } ]
  TravelId;

  @UI.fieldGroup: [ { position: 20, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 10 } ]
  AgencyId;

  @UI.fieldGroup: [ { position: 30, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 20 } ]
  CustomerId;

  @UI.fieldGroup: [ { position: 40, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 40 } ]
  @UI.selectionField: [ { position: 30 } ]
  BeginDate;

  @UI.fieldGroup: [ { position: 50, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 50 } ]
  @UI.selectionField: [ { position: 40 } ]
  EndDate;

  @UI.fieldGroup: [ { position: 60, qualifier: 'TravelDetails' } ]
  BookingFee;

  @UI.fieldGroup: [ { position: 70, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 60 } ]
  @UI.selectionField: [ { position: 50 } ]
  TotalPrice;

  //  CurrencyCode;

  @UI.fieldGroup: [ { position: 80, qualifier: 'TravelDetails' } ]
  @UI.lineItem: [ { position: 70, criticality: 'BeginDateCriticality', criticalityRepresentation: #WITHOUT_ICON } ]
  Description;

  @UI.fieldGroup: [ { position: 90,
                      qualifier: 'TravelDetails',
                      criticality: 'StatusCriticality',
                      criticalityRepresentation: #WITHOUT_ICON } ]
  @UI.lineItem: [ { position: 80, criticality: 'StatusCriticality', criticalityRepresentation: #WITHOUT_ICON } ]
  @UI.selectionField: [ { position: 60 } ]
  Status;

  @UI.fieldGroup: [ { position: 20, qualifier: 'AdminData' } ]
  CreatedBy;

  @UI.fieldGroup: [ { position: 30, qualifier: 'AdminData' } ]
  CreatedAt;

  @UI.fieldGroup: [ { position: 40, qualifier: 'AdminData' } ]
  LastChangedBy;

  @UI.fieldGroup: [ { position: 50, qualifier: 'AdminData' } ]
  LastChangedAt;
}